# Maintainer: Aleksey Filippov <sarum9in@gmail.com>
pkgname='bacs.statement_provider-git'
pkgver=0.0.0
pkgrel=1
pkgdesc="BACS statement_provider interface (library + web site)"
arch=('i686' 'x86_64')
url="https://github.com/bacsorg/statement_provider"
license=('GPL')
groups=()
depends=(
  'bunsan.utility-git'
  'bunsan.web-git'
  'bunsan.pm-git'
  'bunsan.protobuf-git'
  'bacs.problems-git'
  'botan'
)
makedepends=(
  'bunsan.cmake-git'
  'git'
  'boost'
  'turtle'
)
provides=()
conflicts=('bunsan.bacs.statement_provider-git')
replaces=('bunsan.bacs.statement_provider-git')
backup=()
options=()
install=
source=("$pkgname::git+https://github.com/bacsorg/statement_provider.git")
noextract=()
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$pkgname"
  rm -rf build && mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make $MAKEFLAGS
}

check() {
  cd "$srcdir/$pkgname/build"
  make test ARGS=--output-on-failure
}

package() {
  cd "$srcdir/$pkgname/build"
  make DESTDIR="$pkgdir/" install
}
